# Load aliases and shortcuts
[ -f $ZDOTDIR/functions ] && source $ZDOTDIR/functions
[ -f $ZDOTDIR/aliases ] && source $ZDOTDIR/aliases
[ -f $ZDOTDIR/local ] && source $ZDOTDIR/local

# Load colors (for plugins)
autoload -U colors && colors

# Source starship
eval "$(starship init zsh)"

# Source zoxide
eval "$(zoxide init zsh)"

# Source direnv
eval "$(direnv hook zsh)"

# Source autosuggestions
source /usr/share/zsh/plugins/zsh-autosuggestions/zsh-autosuggestions.zsh 2>/dev/null

# fzf settings
source <(fzf --zsh)
export FZF_DEFAULT_COMMAND='fd --type f --hidden --follow --exclude .git'
export FZF_DEFAULT_OPTS="
    --border
    --color=info:#eacb8a,prompt:#bf6069,pointer:#b48dac
    --height 60%
    --layout=reverse
    --multi
    --preview-window=hidden
    --preview='bat {} --color=always'
    --prompt='❯ ' --pointer='▶ ' --marker='✓ '
    --bind 'ctrl-e:execute(echo {+} | xargs -o nvim)'
    --bind 'ctrl-y:execute-silent(echo {+} | xclip -i)'
    --bind 'ctrl-d:preview-down'
    --bind 'ctrl-b:preview-down'
    --bind 'ctrl-u:preview-up'
    --bind 'ctrl-f:preview-up'
    --bind '?:toggle-preview'"

export FZF_ALT_C_OPTS="--preview 'et -I {}'"
export FZF_CTRL_R_OPTS="--preview-window=hidden"

# Load syntax-highlighting; should be last.
source /usr/share/zsh/plugins/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh 2>/dev/null

# Source history completion; should be after zsh-syntax-highlighting
source /usr/share/zsh/plugins/zsh-history-substring-search/zsh-history-substring-search.zsh 2>/dev/null

# SETTINGS

# History options
HISTSIZE=1000000000
SAVEHIST=1000000000
HISTORY_IGNORE="(ls|cd|pwd|exit|cd)*"
HISTFILE="$ZDOTDIR/.zsh_history"
HISTTIMEFORMAT="%h %d %H:%M:%S "

# The meaning of these options can be found in man page of `zshoptions`.
setopt APPEND_HISTORY        # append to history file (Default)
setopt EXTENDED_HISTORY      # Write the history file in the ':start:elapsed;command' format.
setopt HIST_IGNORE_ALL_DUPS  # Delete an old recorded event if a new event is a duplicate.
setopt HIST_IGNORE_DUPS      # Do not record an event that was just recorded again.
setopt HIST_IGNORE_SPACE     # Do not record an event starting with a space.
setopt HIST_REDUCE_BLANKS    # Remove superfluous blanks from each command line being added to the history.
setopt HIST_VERIFY           # Do not execute immediately upon history expansion.
setopt INC_APPEND_HISTORY    # Write to the history file immediately, not when the shell exits.
setopt SHARE_HISTORY         # Share history between all sessions.

# You don't to type cd all the time
setopt AUTO_CD

# Disable Ctrl-S Ctrl-Q
stty -ixon

# Basic tab complete
autoload -Uz compinit; compinit
zstyle ':completion:*' menu select
zstyle ':completion:*' matcher-list '' 'm:{a-zA-Z}={A-Za-z}' 'r:|[._-]=* r:|=*' 'l:|=* r:|=*'
zmodload zsh/complist
_comp_options+=(globdots)

# Use fzf for all tab completions (testing: fzf-tab-source aur package)
# source /usr/share/zsh/plugins/fzf-tab-source/fzf-tab.plugin.zsh

# Add completion for delta (probably needed because of a bug)
compdef _gnu_generic delta

# History search
bindkey '^[[A' history-substring-search-up # Up arrow
bindkey '^[[B' history-substring-search-down # Down arrow

# VIM MODE

# Enable vim mode
bindkey -v
export KEYTIMEOUT=1

# H and L with vim like behavior
bindkey -M vicmd 'H' beginning-of-line
bindkey -M vicmd 'L' end-of-line

# Yank to the system clipboard
__clip_copy() {
  if command -v wl-copy >/dev/null 2>&1; then
    wl-copy
    return 0
  fi

  if command -v pbcopy >/dev/null 2>&1; then
    pbcopy
    return 0
  fi

  if command -v xclip >/dev/null 2>&1; then
    xclip -selection clipboard
    return 0
  fi

  if command -v xsel >/dev/null 2>&1; then
    xsel --clipboard --input
    return 0
  fi

  return 1
}

function vi-yank-to-clipboard() {
  zle vi-yank
  print -rn -- "$CUTBUFFER" | __clip_copy || {
    zle -M "No clipboard tool found (pbcopy/wl-copy/xclip/xsel)."
  }
}

zle -N vi-yank-to-clipboard
bindkey -M vicmd 'y' vi-yank-to-clipboard

# Use vim keys in history search
bindkey -M vicmd 'k' history-substring-search-up
bindkey -M vicmd 'j' history-substring-search-down

# Use vim keys in tab complete menu
bindkey -M menuselect 'h' vi-backward-char
bindkey -M menuselect 'k' vi-up-line-or-history
bindkey -M menuselect 'l' vi-forward-char
bindkey -M menuselect 'j' vi-down-line-or-history

# change fzf bindings
bindkey '^F' fzf-file-widget
bindkey '^H' fzf-history-widget
bindkey '^Z' fzf-cd-widget

bindkey -r '^R'
bindkey -r '^T'
bindkey -r '\eC'

# Change in quotes (vim like)
autoload -U select-quoted
zle -N select-quoted
for m in visual viopp; do
    for c in {a,i}{\',\",\`}; do
        bindkey -M $m $c select-quoted
    done
done

# ci{, ci(, di{ etc..
autoload -U select-bracketed
zle -N select-bracketed
for m in visual viopp; do
    for c in {a,i}${(s..)^:-"()[]{}<>"}; do
        bindkey -M $m $c select-bracketed
    done
done
